plugins {
  id 'idea'
  id 'java'
  id 'jacoco'
  id 'java-library'
  id 'com.github.spotbugs' version '6.2.2'
  id "com.diffplug.spotless" version '7.1.0'
  id 'net.researchgate.release' version '3.1.0'
  id "org.sonarqube" version '6.2.0.5505'
}

group = 'cz.datadriven.utils'

apply from: "${rootProject.projectDir}/gradle/publishing.gradle"

repositories {
  mavenCentral()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

def junitJupiterVersion = '5.13.3'

dependencies {
  api "com.typesafe:config:1.4.4"
  implementation 'net.bytebuddy:byte-buddy:1.17.6'
  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

spotless {
  java {
    licenseHeaderFile 'HEADER'
    // Last version that does not require running on Java 17
    googleJavaFormat("1.24.0")
  }
}

import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
spotbugsMain {
  effort = Effort.valueOf('MAX')
  reportLevel = Confidence.valueOf('LOW')
  reports {
    html {
      required = true
    }
    xml {
      required = false
    }
  }
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
  }
}

release {
  tagTemplate = 'v$version'
}

test {
  useJUnitPlatform()
}

jacocoTestReport {
  reports {
    xml.required = true
  }
}

sonarqube {
  properties {
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.organization', 'datadriven'
    property "sonar.projectKey", "datadrivencz_config-view"
  }
}
