apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
  isReleaseVersion = !version.toString().endsWith("SNAPSHOT")
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  repositories {
    maven {
      name = 'sonatype'
      def releasesRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
      def snapshotsRepoUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
      url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
      credentials {
        username = providers.gradleProperty('nexusUsername').getOrElse('change_me')
        password = providers.gradleProperty('nexusPassword').getOrElse('change_me')
      }
    }
  }
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Datadriven ConfigView'
        description = 'Convenient over typesafe config.'
        url = 'https://github.com/datadrivencz/config-view'
        inceptionYear = '2018'
        scm {
          url = 'https://github.com/datadrivencz/config-view'
          connection = 'scm:https://github.com/datadrivencz/config-view/.git'
          developerConnection = 'scm:git://github.com/datadrivencz/config-view.git'
        }
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = 'dmvk'
            name = 'David Moravek'
            email = 'david.moravek@gmail.com'
          }
          developer {
            id = 'je-ik'
            name = 'Jan Lukavsky'
            email = 'je.ik@seznam.cz'
          }
        }
      }

      from components.java
    }
  }
}

signing {
  useGpgCmd()
  required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
  sign publishing.publications.mavenJava
}
